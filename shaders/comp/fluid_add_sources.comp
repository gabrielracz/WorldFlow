#version 460
#extension GL_GOOGLE_include_directive : enable
#include "../common/grid.comp"
#include "../common/utils.comp"

layout(local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

layout(std430, binding = 0) buffer FluidInfo {
	FluidGridInfo gridInfo;
};

layout(std430, binding = 1) buffer FluidGrid {
	FluidGridCell grid[];
};

layout(push_constant) uniform PushConstants {
	vec4 sourcePosition;
	vec4 velocity;
	vec4 objectPosition;
	float elapsed;
	float dt;
	float sourceRadius;
	int addVelocity;
	int addDensity;
    float density;
	uint objectType;
	float objectRadius;
	float decayRate;
} pc;

float manhattanDistance(vec3 p1, vec3 p2) {
    return abs(p1.x - p2.x) + abs(p1.y - p2.y) + abs(p1.z + p2.z);
}


void main()
{
    uvec3 pos = gl_GlobalInvocationID;
    if (!isWithinGridBounds(pos, gridInfo.resolution)) {
        return;
    }
    uint index = getGridIndex(pos, gridInfo.resolution);

    if(distance(pos, pc.sourcePosition.xyz) < pc.sourceRadius) {
        if(pc.addDensity > 0) {
            grid[index].density += pc.density;
        }

        if(pc.addVelocity > 0) {
            grid[index].velocity += pc.velocity.xyz;
        }
    }

    grid[index].occupied = 0;
    if(distance(pos, pc.objectPosition.xyz) < pc.objectRadius && pc.objectType > 0) {
        grid[index].occupied = pc.objectType;
    }

    grid[index].density *= exp(-pc.decayRate * pc.dt);
    // grid[index].density *= 0.99;
}
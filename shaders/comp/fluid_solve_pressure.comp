#version 460
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_shader_atomic_float : enable
#include "../common/grid.comp"
#include "../common/utils.comp"

layout(local_size_x = GRID_LOCAL_SIZE_X, local_size_y = GRID_LOCAL_SIZE_Y, local_size_z = GRID_LOCAL_SIZE_Z) in;

layout(std430, binding = 0) buffer WorldFlowGridBuffer {
	WorldFlowGrid wfGrid;
};

layout(push_constant) uniform PushConstants {
	float elapsed;
	float dt;
	uint redBlack;
    uint subgridLevel;
} pc;

#define BLOCK_SIZE GRID_LOCAL_SIZE_X
#define CACHE_SIZE BLOCK_SIZE+2
shared float sharedPressure[CACHE_SIZE][CACHE_SIZE][CACHE_SIZE];

void loadSharedCache(uint gridIndex, ivec3 gridPos, ivec3 localPos, ivec3 coarseGridPos) {
    // load current cell's value into cache
	WorldFlowSubGrid grid =  wfGrid.subgrids[pc.subgridLevel].ref;
	float storedPressure = grid.pressureBuffer.data[gridIndex];
    sharedPressure[localPos.z+1][localPos.y+1][localPos.x+1] = storedPressure;
    
	// load halo boundary cells
    for(int d = 0; d < 3; d++) {
        bool loEdge = localPos[d] == 0;
        bool hiEdge = localPos[d] == BLOCK_SIZE-1;
        ivec3 offset = ivec3(0);
        offset[d] = loEdge ? -1 : 1;
        if(loEdge || hiEdge) {
            ivec3 samplePos = gridPos + offset;
            ivec3 cachePos = localPos + (ivec3(1) + offset);
            ivec3 coarseGridSamplePos = coarseGridPos + offset;
            sharedPressure[cachePos.z][cachePos.y][cachePos.x] = pressureBoundary(storedPressure, samplePos, coarseGridSamplePos, wfGrid, pc.subgridLevel);
        }
    }
    barrier();
    memoryBarrierShared();
}


void main()
{
    if(!isWorkgroupActive(wfGrid, pc.subgridLevel, gl_WorkGroupID, gl_NumWorkGroups)) {
        return;
    }

    WorldFlowSubGrid grid = wfGrid.subgrids[pc.subgridLevel].ref;
    uvec3 localPos = gl_LocalInvocationID;
    uvec3 gridPos = getGridPos(wfGrid, pc.subgridLevel, gl_GlobalInvocationID, gl_LocalInvocationID, gl_WorkGroupID, gl_NumWorkGroups);
    uint gridIndex = getGridIndex(gridPos, grid.resolution);
    uvec3 coarseGridPos = getCoarseGridPos(gridPos, wfGrid, pc.subgridLevel);

    loadSharedCache(gridIndex, ivec3(gridPos), ivec3(localPos), ivec3(coarseGridPos));

	if(!isRedBlackActive(gridPos, pc.redBlack)){
		return;
	}

    uvec3 cachePos = localPos + uvec3(1); // offset because of halo
    float r = sharedPressure[cachePos.z][cachePos.y][cachePos.x + 1];
    float l = sharedPressure[cachePos.z][cachePos.y][cachePos.x - 1];
    float u = sharedPressure[cachePos.z][cachePos.y + 1][cachePos.x];
    float d = sharedPressure[cachePos.z][cachePos.y - 1][cachePos.x];
    float f = sharedPressure[cachePos.z + 1][cachePos.y][cachePos.x];
    float b = sharedPressure[cachePos.z - 1][cachePos.y][cachePos.x];
    float h = grid.cellSize;

	const float oldPres = grid.pressureBuffer.data[gridIndex];
    const float newPres = (grid.divergenceBuffer.data[gridIndex] + r + l + u + d + f + b) / 6.0;
    // const float newPres = (grid.divergenceBuffer.data[gridIndex]h*h + r + l + u + d + f + b) / 6.0;

	// SOR
	const float result = SOR(oldPres, newPres, avg3(grid.resolution.xyx));
	// const float result = SOR(oldPres, newPres, 1.7);
	// const float result = newPres;

    // guarded by red-black
    grid.pressureBuffer.data[gridIndex] = result;
	// atomicExchange(grid.pressureBuffer.data[index], result);
}
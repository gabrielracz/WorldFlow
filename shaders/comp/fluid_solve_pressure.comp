#version 460
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_shader_atomic_float : enable
#include "../common/grid.comp"
#include "../common/utils.comp"

layout(local_size_x = GRID_LOCAL_SIZE_X, local_size_y = GRID_LOCAL_SIZE_Y, local_size_z = GRID_LOCAL_SIZE_Z) in;

layout(std430, binding = 0) buffer WorldFlowGridBuffer {
	WorldFlowGrid wfGrid;
};

layout(push_constant) uniform PushConstants {
	float elapsed;
	float dt;
	uint redBlack;
    uint subgridLevel;
} pc;

float samplePressure(WorldFlowSubGrid grid, uvec3 center, ivec3 offset)
{
	ivec3 cell = ivec3(center) + offset;
	bvec3 ge = greaterThanEqual(cell, ivec3(0));
	bvec3 le = lessThan(cell, ivec3(grid.resolution.xyz));
	bool valid = all(ge) && all(le);
	if(!valid) {return grid.pressureBuffer.data[getGridIndex(center, grid.resolution)];}
	if(!valid) {return 0.0;}

	bool occ = (grid.flagsBuffer.data[getGridIndex(cell, grid.resolution)] & FLAG_OCCUPIED) > 0;
	if(occ) {return 0.0;}

	return grid.pressureBuffer.data[getGridIndex(cell, grid.resolution)];
}
void main()
{
    WorldFlowSubGrid grid = wfGrid.subgrids[pc.subgridLevel].ref;
	uvec3 center = gl_GlobalInvocationID;
	if(!isRedBlackActive(center, pc.redBlack)){
		return;
	}
	uint index = getGridIndex(center, grid.resolution);


	const float r = samplePressure(grid, center, ivec3(1, 0, 0));
	const float l = samplePressure(grid, center, ivec3(-1, 0, 0));
	const float u = samplePressure(grid, center, ivec3(0, 1, 0));
	const float d = samplePressure(grid, center, ivec3(0, -1, 0));
	const float f = samplePressure(grid, center, ivec3(0, 0, 1));
	const float b = samplePressure(grid, center, ivec3(0, 0, -1));

	const float oldPres = grid.pressureBuffer.data[index];
    const float newPres = (grid.divergenceBuffer.data[index] + r + l + u + d + f + b) / 6.0;

	// SOR
	const float result = SOR(oldPres, newPres, avg3(grid.resolution.xyx));
	// const float result = newPres;

    // guarded by red-black
    grid.pressureBuffer.data[index] = result;
	// atomicExchange(grid.pressureBuffer.data[index], result);
}
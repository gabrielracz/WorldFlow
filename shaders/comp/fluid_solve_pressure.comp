#version 460
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_shader_atomic_float : enable
#include "../common/grid.comp"

layout(local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

layout(std430, binding = 0) buffer FluidInfo {
	FluidGridInfo gridInfo;
};

layout(std430, binding = 1) buffer FluidGrid {
	FluidGridCell grid[];
};

layout(push_constant) uniform PushConstants {
	float elapsed;
	float dt;
	uint redBlack;
} pc;

float samplePressure(uvec3 center, ivec3 offset)
{
	ivec3 cell = ivec3(center) + offset;
	bvec3 ge = greaterThanEqual(cell, ivec3(0));
	bvec3 le = lessThan(cell, ivec3(gridInfo.resolution.xyz));
	bool occ = checkFlag(grid[getGridIndex(cell, gridInfo.resolution)].flags, FLAG_OCCUPIED);
	bool valid = all(ge) && all(le) && !occ;
	if(!valid) {return grid[getGridIndex(center, gridInfo.resolution)].pressure;}
	// if(!valid) {return 0.0;}
	return grid[getGridIndex(cell, gridInfo.resolution)].pressure;
}

void main()
{
	uvec3 center = gl_GlobalInvocationID;
	// uvec3 center = getRedBlackPosition(gl_GlobalInvocationID, pc.redBlack);
	uint index = getGridIndex(center, gridInfo.resolution);
	FluidGridCell cell = grid[index];

	// Alternate updates in alternating checkerboard pattern to avoid atomics
	if(mod(center.x + center.y + center.z + pc.redBlack, 2) == 0){
		return;
	}

	const float l = samplePressure(center, ivec3(-1, 0, 0));
	const float r = samplePressure(center, ivec3(1, 0, 0));
	const float u = samplePressure(center, ivec3(0, 1, 0));
	const float d = samplePressure(center, ivec3(0, -1, 0));
	const float f = samplePressure(center, ivec3(0, 0, 1));
	const float b = samplePressure(center, ivec3(0, 0, -1));

	const float oldPres = cell.pressure;
    const float newPres = (cell.divergence + l + r + d + u + f + b) / 6.0;

	// SOR
	const float result = SOR(oldPres, newPres, gridInfo.resolution.x);

    // guarded by red-black
    grid[index].pressure = result;
}
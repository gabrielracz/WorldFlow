#version 460
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_shader_atomic_float : enable
#include "../common/grid.comp"

layout(local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

layout(std430, binding = 0) buffer FluidInfo {
	FluidGridInfo gridInfo;
};

layout(std430, binding = 1) buffer FluidGrid {
	FluidGridCell grid[];
};

layout(push_constant) uniform PushConstants {
	float elapsed;
	float dt;
	uint redBlack;
} pc;

vec4 sampleVelocity(uvec3 center, ivec3 offset)
{
	ivec3 cell = ivec3(center) + offset;
	bvec3 ge = greaterThanEqual(cell, ivec3(0));
	bvec3 le = lessThan(cell, ivec3(gridInfo.resolution.xyz));
	bool occ = checkFlag(grid[getGridIndex(cell, gridInfo.resolution)].flags, FLAG_OCCUPIED);
	bool valid = all(ge) && all(le) && !occ;
	// if(!valid) {return grid[getGridIndex(center, gridInfo.resolution)].velocity * abs(vec3(1.0 - abs(vec3(offset))));}
	// if(!valid) {return grid[getGridIndex(center, gridInfo.resolution)].velocity;}
	if(!valid) {return vec4(0.0);}
	return grid[getGridIndex(cell, gridInfo.resolution)].velocity;
}

void main()
{
	uvec3 center = gl_GlobalInvocationID;
	// uvec3 center = getRedBlackPosition(gl_GlobalInvocationID, pc.redBlack);
	uint index = getGridIndex(center, gridInfo.resolution);
	FluidGridCell cell = grid[index];

	// Alternate updates in alternating checkerboard pattern to avoid atomics
	if(mod(center.x + center.y + center.z + pc.redBlack, 2) == 0){
		return;
	}

	const vec4 r = sampleVelocity(center, ivec3(1, 0, 0));
	const vec4 l = sampleVelocity(center, ivec3(-1, 0, 0));
	const vec4 u = sampleVelocity(center, ivec3(0, 1, 0));
	const vec4 d = sampleVelocity(center, ivec3(0, -1, 0));
	const vec4 f = sampleVelocity(center, ivec3(0, 0, 1));
	const vec4 b = sampleVelocity(center, ivec3(0, 0, -1));
	const vec4 c = cell.velocity;

    const float diffRate =  0.000010;
	const float N = 1.0/(gridInfo.cellSize*gridInfo.cellSize);
    const float a = diffRate*pc.dt*N; 

    const vec4 newVel = (c + a*(l + r + u + d + f + b)) / (1+6*a);

	const vec4 result = SOR(cell.velocity, newVel, gridInfo.resolution.x);
	// const vec4 result = newVel;

    // guarded by red-black
    grid[index].velocity = result;

    // atomicExchange(grid[index].velocity.x, result.x);
    // atomicExchange(grid[index].velocity.y, result.y);
    // atomicExchange(grid[index].velocity.z, result.z);
}
#version 460
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_shader_atomic_float : enable
#include "../common/grid.comp"
#include "../common/utils.comp"

layout(local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

layout(std430, binding = 0) buffer FluidInfo {
	FluidGridInfo gridInfo;
};

layout(std430, binding = 1) buffer FluidGridReferencesBuffer {
	FluidGridReferences grid;
};

layout(push_constant) uniform PushConstants {
	float elapsed;
	float dt;
	uint redBlack;
} pc;

float sampleDensity(uvec3 center, ivec3 offset)
{
	ivec3 cell = ivec3(center) + offset;
	bvec3 ge = greaterThanEqual(cell, ivec3(0));
	bvec3 le = lessThan(cell, ivec3(gridInfo.resolution.xyz));
	bool valid = all(ge) && all(le);
	if(!valid) {return grid.densityBuffer.data[getGridIndex(center, gridInfo.resolution)];}

	bool occ = (grid.flagsBuffer.data[getGridIndex(cell, gridInfo.resolution)] & FLAG_OCCUPIED) > 0;
	if(occ) {return grid.densityBuffer.data[getGridIndex(center, gridInfo.resolution)];}

	return grid.densityBuffer.data[getGridIndex(cell, gridInfo.resolution)];
}

void main()
{
	uvec3 center = gl_GlobalInvocationID;
	// uvec3 center = getRedBlackPosition(gl_GlobalInvocationID, pc.redBlack);
	uint index = getGridIndex(center, gridInfo.resolution);

	// Alternate updates in alternating checkerboard pattern to avoid atomics
	if((center.x + center.y + center.z + pc.redBlack % 2) == 0){
		return;
	}

	float l = sampleDensity(center, ivec3(-1, 0, 0));
	float r = sampleDensity(center, ivec3(1, 0, 0));
	float u = sampleDensity(center, ivec3(0, 1, 0));
	float d = sampleDensity(center, ivec3(0, -1, 0));
	float f = sampleDensity(center, ivec3(0, 0, 1));
	float b = sampleDensity(center, ivec3(0, 0, -1));
	float c = grid.densityBuffer.data[index];

    const float diffRate =  0.000015;
	const float N = 1.0/(gridInfo.cellSize*gridInfo.cellSize);
    const float a = diffRate*pc.dt*N; 

    const float newDns = (c + a*(l + r + u + d + f + b)) / (1+6*a); // backwards stable
	const float result = SOR(c, newDns, gridInfo.resolution.x);

	grid.densityBuffer.data[index] = result;
}